prog_start -> prog

prog -> function prog
            | epsilon

function -> FUNCTION identifier SEMICOLON BEGIN_PARAMS declarationloop END_PARAMS BEGIN_LOCALS declarationloop END_LOCALS BEGIN_BODY statementloop END_BODY

declaration -> identifierloop COLON INTEGER
            | identifierloop COLON ARRAY L_SQUARE_BRACKET NUMBER R_SQUARE_BRACKET OF INTEGER

declarationloop -> declaration SEMICOLON declarationloop
            | epsilon

statement -> var ASSIGN expression
            | IF bool_exp THEN statementloop ENDIF
            | IF bool_exp THEN statementloop ELSE statementloop ENDIF
            | WHILE bool_exp BEGINLOOP statementloop ENDLOOP
            | DO BEGINLOOP statementloop ENDLOOP WHILE bool_exp
            | FOR var ASSIGN NUMBER SEMICOLON bool_exp SEMICOLON var ASSIGN expression BEGINLOOP statementloop ENDLOOP
            | READ varloop
            | WRITE varloop
            | CONTINUE
            | RETURN expression

statementloop -> statement SEMICOLON statementloop
            | epsilon

bool_exp -> relation_and_exp OR relation_and_exp
            | relation_and_exp

relation_and_exp -> relation_exp AND relation_exp
            | relation_exp

relation_exp -> notloop expression comp expression
            | TRUE
            | FALSE
            | L_PAREN bool_exp R_PAREN

notloop -> NOT
            | epsilon

comp ->  EQ | NEQ | LT | GT | LTE | GTE

expression -> multiplicative_expression
            | multiplicative_expression ADD expression
            | multiplicative_expression SUB expression

multiplicative_expression -> term
            | term MULT multiplicative_expression
            | term DIV multiplicative_expression
            | term MOD multiplicative_expression

term -> SUB term_num
            | term_num
            | identifier L_PAREN expressionloop R_PAREN

term_num -> var
            | number
            | L_PAREN expression R_PAREN

number-> NUMBER

expressionloop-> expression COMMA expressionloop
            | expression
            | epsilon

varloop -> var | var COMMA varloop

var -> identifier L_SQUARE_BRACKET expression R_SQUARE_BRACKET
            | identifier

identifier -> IDENT

identifierloop -> identifier COMMA identifierloop | identifier